
#Область Обмен_Битрикс24_1С

Процедура ВыполнитьОбмен(Сайт = Неопределено, РучнойОбмен = Ложь) Экспорт 
	
	Отказ = Ложь;
	
	//проверяем что база рабочая 
	Если Не РучнойОбмен
		И РегламентныеЗаданияСервер.РаботаСВнешнимиРесурсамиЗаблокирована() Тогда 
		ЗаписьЖурналаРегистрации("Расширения.ИнтеграцияБитрикс24.ВыполнитьОбмен", УровеньЖурналаРегистрации.Ошибка,,, "Работа с внешними ресурсами заблокирована");
		Возврат
	КонецЕсли;
	
	//проверяем на количество запущенных регламентных заданий
	
	Если Сайт = Неопределено Тогда 
		МассивСайтов = Справочники.b24_СайтИнтеграции.ПолучитьАктивные();
	Иначе 
		
		МассивСайтов = Новый Массив();
		МассивСайтов.Добавить(Сайт);
		
	КонецЕсли; 
	
	Кэш = Новый Структура();
	Кэш.Вставить("Настройки", РегистрыСведений.b24_НастройкиИнтеграций.ПолучитьНастройкиПоМассивуСайтов(МассивСайтов));
	Кэш.Вставить("СостоянияОбменаЗадач", РегистрыСведений.b24_СостоянияОбменаЗадач.ПолучитьСостоянияОбменаЗадач(МассивСайтов));
	Кэш.Вставить("СоответствиеПользователей1С", b24_ОбщегоНазначения.ПолучитьСоответствиеEmailПользователь());   
	Кэш.Вставить("СоответствиеКонтрагентов", b24_ОбщегоНазначения.ПолучитьСоответствиеIdКонтрагент()); 
	
	Для Каждого Сайт Из МассивСайтов Цикл 
		
		Попытка
			
			ОтказПоСайту = Ложь;   
			НачалоОбмена = ТекущаяДатаСеанса();    
			
			//запишем состояние обмена
			РегистрыСведений.b24_НастройкиИнтеграций.ЗаписатьСостояниеОбмена(Сайт, Истина);
			
			//удалим ранее заблокированные объекты, теперь их можно загружать
			РегистрыСведений.b24_ЗаблокированныеОбъектыДляЗагрузки.Очистить(Сайт);
			
			//если это тестовая база, то не осуществляем выгрузку
			Если Не РегламентныеЗаданияСервер.РаботаСВнешнимиРесурсамиЗаблокирована() Тогда 
				b24_Интеграция.ВыполнитьВыгрузкуОбязательныхРеквизитов(Сайт, Кэш, Отказ);
				b24_Интеграция.ВыполнитьВыгрузку(Сайт,, Кэш, Отказ); 
			КонецЕсли;
			
			b24_Интеграция.ВыполнитьЗагрузку(Сайт, Кэш, ОтказПоСайту); 
			
			//Записываем дату обмена на начало выполненения обмена, для дальнейшей фильтрации 
			КоличествоВыполненныхЗапросов 	= 0;
			
			Если Кэш.Настройки[Сайт] <> Неопределено
				И Кэш.Настройки[Сайт].Свойство("ПараметрыПодключения")
				И Кэш.Настройки[Сайт].ПараметрыПодключения["КоличествоВыполненныхЗапросов"] <> Неопределено Тогда 
				
				КоличествоВыполненныхЗапросов = Кэш.Настройки[Сайт].ПараметрыПодключения["КоличествоВыполненныхЗапросов"];
				
			КонецЕсли;
			
			РегистрыСведений.b24_НастройкиИнтеграций.ЗаписатьДанныеОбмена(Сайт, НачалоОбмена, ТекущаяДатаСеанса(), КоличествоВыполненныхЗапросов);       
			
			//запишем состояние обмена
			РегистрыСведений.b24_НастройкиИнтеграций.ЗаписатьСостояниеОбмена(Сайт, Ложь);
			
		Исключение
			
			//запишем состояние обмена
			РегистрыСведений.b24_НастройкиИнтеграций.ЗаписатьСостояниеОбмена(Сайт, Ложь); 
			
			ОтказПоСайту = Истина;
			ЗаписьЖурналаРегистрации("Расширения.ИнтеграцияБитрикс24.ВыполнитьОбмен", 
			УровеньЖурналаРегистрации.Ошибка,,, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке())); 
			
		КонецПопытки;		
	КонецЦикла; 
		
КонецПроцедуры 

#КонецОбласти

