
Функция ПолучитьМассивВыгружаемыхОбъектов(Сайт) Экспорт 
	
	Массив = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	b24_ВыгружаемыеОбъекты.Ссылка КАК Ссылка
		|ИЗ
		|	РегистрСведений.b24_ВыгружаемыеОбъекты КАК b24_ВыгружаемыеОбъекты
		|ГДЕ
		|	b24_ВыгружаемыеОбъекты.Сайт = &Сайт";
	
	Запрос.УстановитьПараметр("Сайт", Сайт);
	
	РезультатЗапроса 		= Запрос.Выполнить();
	ВыборкаДетальныеЗаписи 	= РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Массив.Добавить(ВыборкаДетальныеЗаписи.Ссылка);
	КонецЦикла;
	
	Возврат Массив
	
КонецФункции  

Процедура Записать(Сайт, Ссылка) Экспорт 
	
	МенеджерЗаписи 			= РегистрыСведений.b24_ВыгружаемыеОбъекты.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Сайт 	= Сайт;
	МенеджерЗаписи.Ссылка 	= Ссылка;
	
	МенеджерЗаписи.Записать();
	
КонецПроцедуры 

Процедура Удалить(Сайт, Ссылка) Экспорт 
	
	НаборЗаписей	= РегистрыСведений.b24_ВыгружаемыеОбъекты.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Сайт.Установить(Сайт);
	НаборЗаписей.Отбор.Ссылка.Установить(Ссылка);
	НаборЗаписей.Записать();
	
КонецПроцедуры

Процедура ИзменитьСайт(Сайт, Задача) Экспорт 
		
	ТекущийСайт 		= Справочники.b24_СайтИнтеграции.ПустаяСсылка();  
	ТекущийМассивСайтов = РегистрыСведений.b24_СостоянияОбменаЗадач.ПолучитьМассивСайтовЗадачи(Задача);  
	
	Если ТекущийМассивСайтов.Количество() > 0 Тогда 
		ТекущийСайт 		= ТекущийМассивСайтов[0];
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ТекущийСайт) Тогда 
		
		МенеджерЗаписи 			= РегистрыСведений.b24_ВыгружаемыеОбъекты.СоздатьМенеджерЗаписи();
		МенеджерЗаписи.Сайт 	= ТекущийСайт;
		МенеджерЗаписи.Ссылка 	= Задача; 
		МенеджерЗаписи.Прочитать();
		
		Если МенеджерЗаписи.Выбран() Тогда 
			РегистрыСведений.b24_ВыгружаемыеОбъекты.Удалить(ТекущийСайт, Задача); 
			РегистрыСведений.b24_ВыгружаемыеОбъекты.Записать(Сайт, Задача);  
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры
