
Функция ПолучитьСостоянияОбменаЗадач(МассивСайтов, МассивЗадач = Неопределено) Экспорт 

	СоответствиеСайт = Новый Соответствие;

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	b24_СостоянияОбменаЗадач.Сайт КАК Сайт,
		|	b24_СостоянияОбменаЗадач.Задача.Код КАК ЗадачаКод,
		|	b24_СостоянияОбменаЗадач.Задача КАК Задача,
		|	b24_СостоянияОбменаЗадач.ДатаПоследнегоУспешногоОбмена КАК ДатаПоследнегоУспешногоОбмена,
		|	b24_СостоянияОбменаЗадач.Задача.НомерВнешнейЗаявки КАК НомерВнешнейЗаявки
		|ИЗ
		|	РегистрСведений.b24_СостоянияОбменаЗадач КАК b24_СостоянияОбменаЗадач
		|ГДЕ
		|	b24_СостоянияОбменаЗадач.Сайт В(&МассивСайтов)
		|	И ВЫБОР
		|			КОГДА &ИспользоватьОтборПоЗадачам
		|				ТОГДА b24_СостоянияОбменаЗадач.Задача В (&МассивЗадач)
		|			ИНАЧЕ ИСТИНА
		|		КОНЕЦ
		|ИТОГИ ПО
		|	Сайт";
	
	Запрос.УстановитьПараметр("МассивСайтов", МассивСайтов);
	Запрос.УстановитьПараметр("МассивЗадач", МассивЗадач);
	Запрос.УстановитьПараметр("ИспользоватьОтборПоЗадачам", МассивЗадач <> Неопределено И МассивЗадач.Количество() > 0);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаСайт = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаСайт.Следующий() Цикл
		
		Соответствие 		= Новый Соответствие;
		ВыборкаДетальная 	= ВыборкаСайт.Выбрать();
		
		Пока ВыборкаДетальная.Следующий() Цикл 
			
			Состояние = Новый Структура();
			Состояние.Вставить("ДатаПоследнегоУспешногоОбмена", ВыборкаДетальная.ДатаПоследнегоУспешногоОбмена); 
			
			Соответствие[ВыборкаДетальная.НомерВнешнейЗаявки] = Состояние; 		
			
		КонецЦикла;   
		
		СоответствиеСайт[ВыборкаСайт.Сайт] = Соответствие; 		
		
	КонецЦикла;
		
	Возврат СоответствиеСайт
	
КонецФункции  

Функция ПолучитьМассивСайтовЗадачи(Задача) Экспорт 
	
	Массив = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	b24_СостоянияОбменаЗадач.Сайт КАК Сайт
		|ИЗ
		|	РегистрСведений.b24_СостоянияОбменаЗадач КАК b24_СостоянияОбменаЗадач
		|ГДЕ
		|	b24_СостоянияОбменаЗадач.Задача = &Задача
		|
		|СГРУППИРОВАТЬ ПО
		|	b24_СостоянияОбменаЗадач.Сайт";
	
	Запрос.УстановитьПараметр("Задача", Задача);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Массив.Добавить(ВыборкаДетальныеЗаписи.Сайт);
	КонецЦикла;
	
	Возврат Массив
	
КонецФункции

Функция ДатаПоследнегоУспешногоОбмена(Сайт, Задача) Экспорт 

	Результат = Неопределено;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	b24_СостоянияОбменаЗадач.ДатаПоследнегоУспешногоОбмена КАК ДатаПоследнегоУспешногоОбмена
		|ИЗ
		|	РегистрСведений.b24_СостоянияОбменаЗадач КАК b24_СостоянияОбменаЗадач
		|ГДЕ
		|	b24_СостоянияОбменаЗадач.Сайт = &Сайт
		|	И b24_СостоянияОбменаЗадач.Задача = &Задача";
	
	Запрос.УстановитьПараметр("Задача", Задача);
	Запрос.УстановитьПараметр("Сайт", Сайт);
	
	РезультатЗапроса 	= Запрос.Выполнить();
	Выборка			 	= РезультатЗапроса.Выбрать();
	
	Если Выборка.Следующий() Тогда 
		Результат = Выборка.ДатаПоследнегоУспешногоОбмена; 
	КонецЕсли;
		
	Возврат Результат	
	
КонецФункции

Процедура Записать(Сайт, Задача, Дата) Экспорт 
	
	МенеджерЗаписи 									= РегистрыСведений.b24_СостоянияОбменаЗадач.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Сайт 							= Сайт;
	МенеджерЗаписи.Задача 							= Задача;
	МенеджерЗаписи.ДатаПоследнегоУспешногоОбмена 	= Дата; 
	
	МенеджерЗаписи.Записать();
	
КонецПроцедуры

Процедура Удалить(Сайт, Задача) Экспорт 
	
	НаборЗаписей	= РегистрыСведений.b24_СостоянияОбменаЗадач.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Сайт.Установить(Сайт);
	НаборЗаписей.Отбор.Задача.Установить(Задача);
	НаборЗаписей.Записать();
	
КонецПроцедуры

Процедура ОчиститьСостояниеОбменаСайта(Сайт) Экспорт 
	
	НаборЗаписей = РегистрыСведений.b24_СостоянияОбменаЗадач.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Сайт.Установить(Сайт); 
	НаборЗаписей.Записать();	
	
КонецПроцедуры 

Процедура ИзменитьСайт(Сайт, Задача) Экспорт 
	
	ТекущийСайт 		= Справочники.b24_СайтИнтеграции.ПустаяСсылка();  
	ТекущийМассивСайтов = РегистрыСведений.b24_СостоянияОбменаЗадач.ПолучитьМассивСайтовЗадачи(Задача);  
	
	Если ТекущийМассивСайтов.Количество() > 0 Тогда 
		ТекущийСайт 		= ТекущийМассивСайтов[0];
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ТекущийСайт) Тогда  
		
		ДатаПоследнегоУспешногоОбмена = РегистрыСведений.b24_СостоянияОбменаЗадач.ДатаПоследнегоУспешногоОбмена(ТекущийСайт, Задача);
		
		Если ЗначениеЗаполнено(ДатаПоследнегоУспешногоОбмена) Тогда 
			РегистрыСведений.b24_СостоянияОбменаЗадач.Удалить(ТекущийСайт, Задача); 
			РегистрыСведений.b24_СостоянияОбменаЗадач.Записать(Сайт, Задача, ДатаПоследнегоУспешногоОбмена);  
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры
