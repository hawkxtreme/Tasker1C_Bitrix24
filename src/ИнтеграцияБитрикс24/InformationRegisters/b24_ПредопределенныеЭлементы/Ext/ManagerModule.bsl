
Функция ПолучитьЗначениеПоИмени(Имя, СообщитьОбОтсутствии = Ложь, ЗначениеПоУмолчанию = Неопределено) Экспорт 
	
	Значение = ЗначениеПоУмолчанию;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Имя", Имя);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	b24_ПредопределенныеЭлементы.Значение
	|ИЗ
	|	РегистрСведений.b24_ПредопределенныеЭлементы КАК b24_ПредопределенныеЭлементы
	|ГДЕ
	|	b24_ПредопределенныеЭлементы.Имя = &Имя";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если Не РезультатЗапроса.Пустой() Тогда
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		ВыборкаДетальныеЗаписи.Следующий();
		
		Значение = ВыборкаДетальныеЗаписи.Значение;
	КонецЕсли;
	
	Если СообщитьОбОтсутствии И Не ЗначениеЗаполнено(Значение) Тогда
		Сообщить(СтрШаблон("Предопределенное значение с именем %1 не заполнено", Имя));			
	КонецЕсли; 
	
	Возврат Значение
	
КонецФункции

Функция ПолучитьМассивЗначенийПоИмени(Имя, СообщитьОбОтсутствии = Ложь) Экспорт 
	
	Массив = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Имя", Имя);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	b24_ПредопределенныеЭлементы.Значение
	|ИЗ
	|	РегистрСведений.b24_ПредопределенныеЭлементы КАК b24_ПредопределенныеЭлементы
	|ГДЕ
	|	b24_ПредопределенныеЭлементы.Имя = &Имя";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если Не РезультатЗапроса.Пустой() Тогда 
		Массив = РезультатЗапроса.Выгрузить().ВыгрузитьКолонку("Значение");
	КонецЕсли;
	
	Если СообщитьОбОтсутствии И Не ЗначениеЗаполнено(Массив) Тогда
		Сообщить(СтрШаблон("Предопределенное значение с именем %1 не заполнено", Имя));			
	КонецЕсли; 
	
	Возврат Массив
	
КонецФункции

Функция ПолучитьМассивЗначенийПоИменам(СписокКлючей = Неопределено, ЗначениеВМассив = Ложь) Экспорт 
	
	Результат = Новый Соответствие;
	
	Если ЗначениеЗаполнено(СписокКлючей) Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	b24_ПредопределенныеЭлементы.Значение КАК Значение,
		|	b24_ПредопределенныеЭлементы.Имя КАК Ключ
		|ИЗ
		|	РегистрСведений.b24_ПредопределенныеЭлементы КАК b24_ПредопределенныеЭлементы
		|ГДЕ
		|	b24_ПредопределенныеЭлементы.Имя В (&SevenKeys)
		|";
		
		Если ТипЗнч(СписокКлючей) = Тип("Массив") Тогда
			Запрос.УстановитьПараметр("SevenKeys",СписокКлючей);
		КонецЕсли;	
		
		Если ТипЗнч(СписокКлючей) = Тип("Строка") Тогда
			МассивСтрок = Новый Массив();
			ВходящаяСтрока = СтрЗаменить(СписокКлючей,",",Символы.ПС);
			
			Для ИндексСтроки = 1 По СтрЧислоСтрок(ВходящаяСтрока) Цикл
				Подстрока = СтрПолучитьСтроку(ВходящаяСтрока, ИндексСтроки);
				МассивСтрок.Добавить(Подстрока);
			КонецЦикла;
			Запрос.УстановитьПараметр("SevenKeys",МассивСтрок);
		КонецЕсли;	
	Иначе	
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1024
		|	b24_ПредопределенныеЭлементы.Значение КАК Значение,
		|	b24_ПредопределенныеЭлементы.Имя КАК Ключ
		|ИЗ
		|	РегистрСведений.b24_ПредопределенныеЭлементы КАК b24_ПредопределенныеЭлементы
		|";
		
	КонецЕсли;
	
	РезультатЗапроса = Запрос.Выполнить().Выгрузить();
	
	Если ЗначениеВМассив Тогда
		РезультатЗапроса.Сортировать("Ключ");
		ТекущийКлюч = Неопределено;
		Значение = Новый Массив;
		Для Каждого Стр Из РезультатЗапроса Цикл
			Если ТекущийКлюч <> Стр.Ключ Тогда
				Если ТекущийКлюч <> Неопределено Тогда
					Результат.Вставить(ТекущийКлюч, Значение);
				КонецЕсли;				
				ТекущийКлюч = Стр.Ключ;
				Значение = Новый Массив;
			КонецЕсли;			
			Значение.Добавить(Стр.Значение);
		КонецЦикла;	
		// Добавление последнего ключа
		Если ТекущийКлюч <> Неопределено Тогда
			Результат.Вставить(ТекущийКлюч, Значение);
		КонецЕсли;				
	Иначе		
		Для Каждого Стр Из РезультатЗапроса Цикл
			Результат.Вставить(Стр.Ключ,Стр.Значение);
		КонецЦикла;	
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции
