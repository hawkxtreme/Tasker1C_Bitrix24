
&После("ПередЗаписью")
Процедура b24_ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка
		И ДополнительныеСвойства.Свойство("ЗадачаЗагруженаИзРасширенияИнтеграцияБитрикс24")
		И ДополнительныеСвойства.ЗадачаЗагруженаИзРасширенияИнтеграцияБитрикс24 = Истина Тогда
		
		Если ЭтоНовый()
			И НЕ ЗначениеЗаполнено(Родитель) Тогда
			ЭтоОсновнаяЗадача = Истина;
		Конецесли;
		
		Если НЕ ЭтоОсновнаяЗадача Тогда
			Проект = Неопределено;
		КонецЕсли;
		
		ОсновнаяЗадача = ПолучитьОсновнуюЗадачу(Родитель); 
		
		ЗаполнитьСтатусЗадачиЕслиОнНеЗаполнен(Отказ);
		
		СобытияВИстории = ОбновитьИсторию();
		
		ДополнительныеСвойства.Вставить("СобытияВИстории",СобытияВИстории);
		
		КоличествоЗадачЧекЛиста = ЭтотОбъект.Комментарии.Количество();
		КоличествоВыполненныхЗадачЧекЛиста = ЭтотОбъект.Комментарии.Итог("Выполнено");   
		                                                                                 		
	КонецЕсли;                                                                           
	
	Если ЗначениеЗаполнено(Исполнитель) Тогда 
		
		ТаблицаКонтрагентов = РегистрыСведений.b24_ИдентификаторыОбъектов.ПолучитьТаблицуКонтрагентов(Исполнитель);
		
		Если ТаблицаКонтрагентов.Количество() > 0 Тогда 
			b24_ПодразделениеИсполнитель = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ТаблицаКонтрагентов[0].Контрагент, "b24_Подразделение");
		КонецЕсли;
		
	ИначеЕсли ЭтоНовый()
		И ДополнительныеСвойства.Свойство("b24_ЭтоИнтерактивноеИзменение")
		И ДополнительныеСвойства.b24_ЭтоИнтерактивноеИзменение
		И Не ЗначениеЗаполнено(b24_ПодразделениеИсполнитель) Тогда 
		
		ТаблицаКонтрагентов = РегистрыСведений.b24_ИдентификаторыОбъектов.ПолучитьТаблицуКонтрагентов(Автор);
		
		Если ТаблицаКонтрагентов.Количество() > 0 Тогда 
			b24_ПодразделениеИсполнитель = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ТаблицаКонтрагентов[0].Контрагент, "b24_Подразделение");
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&После("ПриЗаписи")
Процедура b24_ПриЗаписи(Отказ)
	
	//фиксируем задачу для выгрузки
	b24_Интеграция.ЗаписатьОбъектВВыгружаемыеОбъекты(ЭтотОбъект, Отказ);
	
КонецПроцедуры

