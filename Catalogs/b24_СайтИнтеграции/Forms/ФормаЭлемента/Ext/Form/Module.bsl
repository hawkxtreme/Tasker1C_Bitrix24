	
#Область Форма

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ТаблицаОбменаЭталон.Загрузить(Справочники.b24_СайтИнтеграции.ПолучитьОбъектыОбмена());
	
	Если Объект.ОбъектыОбмена.Количество() = 0 Тогда 
		Модифицированность = Истина;
		Объект.ОбъектыОбмена.Загрузить(ТаблицаОбменаЭталон.Выгрузить());
	КонецЕсли;
	
КонецПроцедуры    

#КонецОбласти

#Область ЭлементыФормы

&НаКлиенте
Процедура ОбъектыОбменаЗагрузкаПриИзменении(Элемент)
	ОбъектыОбменаПриИзмененииНаСервере("Загрузка");
КонецПроцедуры

&НаКлиенте
Процедура ОбъектыОбменаВыгрузкаПриИзменении(Элемент)
	ОбъектыОбменаПриИзмененииНаСервере("Выгрузка");
КонецПроцедуры

&НаСервере
Процедура ОбъектыОбменаПриИзмененииНаСервере(Колонка)
	
	ТекущиеДанные = Объект.ОбъектыОбмена.НайтиПоИдентификатору(Элементы.ОбъектыОбмена.ТекущаяСтрока);
	
	Если ТекущиеДанные <> Неопределено Тогда 
		
		ИскомыеСтроки = ТаблицаОбменаЭталон.НайтиСтроки(Новый Структура("Объект", ТекущиеДанные.Объект)); 
		
		Если ИскомыеСтроки.Количество() > 0
			И ИскомыеСтроки[0][Колонка] = Ложь
			И ТекущиеДанные[Колонка] = Истина Тогда 
			ТекущиеДанные[Колонка] = Ложь;	
		КонецЕсли;		
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ПользовательскиеПоля_ЗадачиВнешнийПутьНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Если Объект.Ссылка.Пустая() Тогда 
		
		ОписаниеОповещения = Новый ОписаниеОповещения("ПользовательскиеПоля_ЗадачиВнешнийПутьНачалоВыбораЗавершениеВопрос", ЭтаФорма);
		
		ПоказатьВопрос(ОписаниеОповещения, "Настройки не сохранены. Записать?", РежимДиалогаВопрос.ДаНет,, КодВозвратаДиалога.Да, "Внимание"); 
		
	Иначе 
		ПользовательскиеПоля_ЗадачиВнешнийПутьНачалоВыбораПродолжение();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПользовательскиеПоля_ЗадачиВнешнийПутьНачалоВыбораЗавершениеВопрос(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда 
		Записать();
	КонецЕсли; 
	
	Если Не Объект.Ссылка.Пустая() Тогда 
		ПользовательскиеПоля_ЗадачиВнешнийПутьНачалоВыбораПродолжение();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПользовательскиеПоля_ЗадачиВнешнийПутьНачалоВыбораПродолжение()
	
	Идентификатор	= "";
	ТекущиеДанные 	= Элементы.ПользовательскиеПоля_Задачи.ТекущиеДанные;
	
	Если ТекущиеДанные <> Неопределено Тогда 
		Идентификатор = ТекущиеДанные.ВнешнийПуть;
	КонецЕсли;
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("Сайт", Объект.Ссылка);
	ПараметрыОткрытия.Вставить("Идентификатор", Идентификатор); 
	
	ОткрытьФорму("ОбщаяФорма.b24_ФормаВыбораПользовательскихПолей", ПараметрыОткрытия, ЭтаФорма, УникальныйИдентификатор,,, Новый ОписаниеОповещения("ПользовательскиеПоля_ЗадачиВнешнийПутьНачалоВыбораЗавершение", ЭтаФорма), );

КонецПроцедуры

&НаКлиенте
Процедура ПользовательскиеПоля_ЗадачиВнешнийПутьНачалоВыбораЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	ТекущиеДанные 	= Элементы.ПользовательскиеПоля_Задачи.ТекущиеДанные;
	
	Если ТекущиеДанные <> Неопределено
		И Результат <> Неопределено Тогда 
		
		ТекущиеДанные.ВнешнийПуть 	= Результат.FIELD;
		ТекущиеДанные.Тип 			= Результат.TYPE;
		
		Модифицированность 			= Истина;
		
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ПользовательскиеПоля_ЗадачиВнутреннийПутьНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Если Объект.Ссылка.Пустая() Тогда 
		
		ОписаниеОповещения = Новый ОписаниеОповещения("ПользовательскиеПоля_ЗадачиВнутреннийПутьНачалоВыбораЗавершениеВопрос", ЭтаФорма);
		
		ПоказатьВопрос(ОписаниеОповещения, "Настройки не сохранены. Записать?", РежимДиалогаВопрос.ДаНет,, КодВозвратаДиалога.Да, "Внимание"); 
		
	Иначе 
		ПользовательскиеПоля_ЗадачиВнутреннийПутьНачалоВыбораПродолжение();
	КонецЕсли;
	
КонецПроцедуры 

&НаКлиенте
Процедура ПользовательскиеПоля_ЗадачиВнутреннийПутьНачалоВыбораЗавершениеВопрос(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда 
		Записать();
	КонецЕсли; 
	
	Если Не Объект.Ссылка.Пустая() Тогда 
		ПользовательскиеПоля_ЗадачиВнутреннийПутьНачалоВыбораПродолжение();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПользовательскиеПоля_ЗадачиВнутреннийПутьНачалоВыбораПродолжение()
	
	МассивРеквизитов 	= Новый Массив;
	ТекущиеДанные 		= Элементы.ПользовательскиеПоля_Задачи.ТекущиеДанные;
	
	Если ТекущиеДанные <> Неопределено
		И ЗначениеЗаполнено(ТекущиеДанные.ВнутреннийПуть) Тогда 
		МассивРеквизитов.Добавить(ТекущиеДанные.ВнутреннийПуть);	
	КонецЕсли;
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("Ссылка", ПредопределенноеЗначение("Справочник.узЗадачи.ПустаяСсылка")); 
	ПараметрыОткрытия.Вставить("Отбор", МассивРеквизитов);
	
	ОткрытьФорму("ОбщаяФорма.b24_ФормаВыборРеквизитаШапкиОбъекта", ПараметрыОткрытия, ЭтаФорма, УникальныйИдентификатор,,, Новый ОписаниеОповещения("ПользовательскиеПоля_ЗадачиВнутреннийПутьНачалоВыбораЗавершение", ЭтаФорма), );

КонецПроцедуры

&НаКлиенте
Процедура ПользовательскиеПоля_ЗадачиВнутреннийПутьНачалоВыбораЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	ТекущиеДанные 		= Элементы.ПользовательскиеПоля_Задачи.ТекущиеДанные;
	
	Если Результат <> Неопределено
		И ТекущиеДанные <> Неопределено
		И Результат.Количество() > 0 Тогда 
		ТекущиеДанные.ВнутреннийПуть = Результат.ВыбранныеРеквизиты[0];
	КонецЕсли;	
	
КонецПроцедуры

#КонецОбласти
