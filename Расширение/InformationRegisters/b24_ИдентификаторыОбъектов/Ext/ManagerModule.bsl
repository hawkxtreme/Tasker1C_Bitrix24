
Процедура Обновить(Сайт, Ссылка, Знач ID) Экспорт 
	
	Если ТипЗнч(ID) = Тип("Строка")
		И СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(ID) Тогда 
		ID = СтроковыеФункцииКлиентСервер.СтрокаВЧисло(ID);
	ИначеЕсли ТипЗнч(ID) = Тип("Число") Тогда 
		
	Иначе 
		ВызватьИсключение "ID может быть только числом или строкой которую можно преобразовать в строку";
	КонецЕсли; 
	
	Если Не ЕстьИдентификатор(Сайт, Ссылка) Тогда 
		
		МенеджерЗаписи 			= РегистрыСведений.b24_ИдентификаторыОбъектов.СоздатьМенеджерЗаписи();
		МенеджерЗаписи.Сайт 	= Сайт;
		МенеджерЗаписи.Ссылка 	= Ссылка;
		МенеджерЗаписи.ID 		= ID;
		МенеджерЗаписи.Записать();
		
	КонецЕсли;
	
КонецПроцедуры

Функция ЕстьИдентификатор(Сайт, Ссылка) Экспорт 
	
	Результат = Ложь;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	b24_ИдентификаторыОбъектов.ID КАК ID
		|ИЗ
		|	РегистрСведений.b24_ИдентификаторыОбъектов КАК b24_ИдентификаторыОбъектов
		|ГДЕ
		|	b24_ИдентификаторыОбъектов.Сайт = &Сайт
		|	И b24_ИдентификаторыОбъектов.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Сайт", Сайт);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить(); 
	
	Если Не РезультатЗапроса.Пустой() Тогда 
		Результат = Истина;	
	КонецЕсли;
	
	Возврат Результат
	
КонецФункции 

Функция Идентификатор(Сайт, Ссылка) Экспорт 
	
	Результат = Неопределено;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	b24_ИдентификаторыОбъектов.ID КАК ID
		|ИЗ
		|	РегистрСведений.b24_ИдентификаторыОбъектов КАК b24_ИдентификаторыОбъектов
		|ГДЕ
		|	b24_ИдентификаторыОбъектов.Сайт = &Сайт
		|	И b24_ИдентификаторыОбъектов.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Сайт", Сайт);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса 	= Запрос.Выполнить(); 
	Выборка				= РезультатЗапроса.Выбрать();
	
	Если Выборка.Следующий() Тогда 
		Результат = Выборка.ID;	
	КонецЕсли;
	
	Возврат Результат
	
КонецФункции 

Функция ПолучитьТаблицуКонтрагентов(Пользователь) Экспорт 
	
	Результат = Новый ТаблицаЗначений;
	Результат.Колонки.Добавить("Сайт");
	Результат.Колонки.Добавить("Контрагент");
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	b24_ИдентификаторыОбъектов.Сайт КАК Сайт,
		|	b24_ИдентификаторыОбъектов.ID КАК ID
		|ПОМЕСТИТЬ ВТ_Пользователи
		|ИЗ
		|	РегистрСведений.b24_ИдентификаторыОбъектов КАК b24_ИдентификаторыОбъектов
		|ГДЕ
		|	b24_ИдентификаторыОбъектов.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	b24_ИдентификаторыОбъектов.Сайт КАК Сайт,
		|	b24_ИдентификаторыОбъектов.Ссылка КАК Ссылка
		|ИЗ
		|	ВТ_Пользователи КАК ВТ_Пользователи
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.b24_ИдентификаторыОбъектов КАК b24_ИдентификаторыОбъектов
		|		ПО ВТ_Пользователи.Сайт = b24_ИдентификаторыОбъектов.Сайт
		|			И ВТ_Пользователи.ID = b24_ИдентификаторыОбъектов.ID
		|			И (b24_ИдентификаторыОбъектов.Ссылка ССЫЛКА Справочник.узКонтрагенты)";
	
	Запрос.УстановитьПараметр("Ссылка", Пользователь);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		НоваяСтрока 			= Результат.Добавить();
		НоваяСтрока.Сайт 		= ВыборкаДетальныеЗаписи.Сайт;
		НоваяСтрока.Контрагент 	= ВыборкаДетальныеЗаписи.Ссылка;
		
	КонецЦикла;
	
	Возврат Результат
	
КонецФункции 

Процедура Удалить(Сайт, Ссылка) Экспорт 
	
	НаборЗаписей	= РегистрыСведений.b24_ИдентификаторыОбъектов.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Сайт.Установить(Сайт);
	НаборЗаписей.Отбор.Ссылка.Установить(Ссылка);
	НаборЗаписей.Записать();
	
КонецПроцедуры

Процедура ИзменитьСайт(Сайт, Задача) Экспорт 
		
	ТекущийСайт 		= Справочники.b24_СайтИнтеграции.ПустаяСсылка();  
	ТекущийМассивСайтов = РегистрыСведений.b24_СостоянияОбменаЗадач.ПолучитьМассивСайтовЗадачи(Задача);  
	
	Если ТекущийМассивСайтов.Количество() > 0 Тогда 
		ТекущийСайт 		= ТекущийМассивСайтов[0];
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ТекущийСайт) Тогда  
		
		Идентификатор			= РегистрыСведений.b24_ИдентификаторыОбъектов.Идентификатор(ТекущийСайт, Задача);
		
		Если ЗначениеЗаполнено(Идентификатор) Тогда 
			РегистрыСведений.b24_ИдентификаторыОбъектов.Удалить(ТекущийСайт, Задача); 
			РегистрыСведений.b24_ИдентификаторыОбъектов.Обновить(Сайт, Задача, Идентификатор);  
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры
