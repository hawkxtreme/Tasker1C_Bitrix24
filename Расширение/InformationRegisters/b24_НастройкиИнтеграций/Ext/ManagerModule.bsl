
Функция ПолучитьНастройкиПоМассивуСайтов(Массив) Экспорт 
	
	Соответствие = Новый Соответствие;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	b24_НастройкиИнтеграций.Сайт КАК Сайт,
		|	b24_НастройкиИнтеграций.РодительскийКаталог КАК РодительскийКаталог,
		|	b24_НастройкиИнтеграций.СтатусПоУмолчанию КАК СтатусПоУмолчанию,
		|	b24_НастройкиИнтеграций.СтатусПовторногоВозобновленияЗадачи КАК СтатусПовторногоВозобновленияЗадачи,
		|	b24_НастройкиИнтеграций.ПользовательОбмена КАК ПользовательОбмена,
		|	b24_НастройкиИнтеграций.ВидЗаказчика КАК ВидЗаказчика,
		|	b24_НастройкиИнтеграций.Подразделение КАК Подразделение,
		|	b24_НастройкиИнтеграций.ДатаНачалаОбмена КАК ДатаНачалаОбмена,
		|	b24_НастройкиИнтеграций.ДатаПоследнегоУспешногоОбмена КАК ДатаПоследнегоУспешногоОбмена
		|ИЗ
		|	РегистрСведений.b24_НастройкиИнтеграций КАК b24_НастройкиИнтеграций
		|ГДЕ
		|	b24_НастройкиИнтеграций.Сайт В(&Массив)";
	
	Запрос.УстановитьПараметр("Массив", Массив);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		Настройка = Новый Структура();
		Настройка.Вставить("РодительскийКаталог", ВыборкаДетальныеЗаписи.РодительскийКаталог);
		Настройка.Вставить("СтатусПоУмолчанию", ВыборкаДетальныеЗаписи.СтатусПоУмолчанию);
		Настройка.Вставить("СтатусПовторногоВозобновленияЗадачи", ВыборкаДетальныеЗаписи.СтатусПовторногоВозобновленияЗадачи);
		Настройка.Вставить("ВидЗаказчика", ВыборкаДетальныеЗаписи.ВидЗаказчика);
		Настройка.Вставить("ПользовательОбмена", ВыборкаДетальныеЗаписи.ПользовательОбмена);
		Настройка.Вставить("Подразделение", ВыборкаДетальныеЗаписи.Подразделение);
		Настройка.Вставить("ДатаНачалаОбмена", ВыборкаДетальныеЗаписи.ДатаНачалаОбмена);
		Настройка.Вставить("ДатаПоследнегоУспешногоОбмена", ВыборкаДетальныеЗаписи.ДатаПоследнегоУспешногоОбмена);   
		Настройка.Вставить("ПараметрыПодключения", Справочники.b24_СайтИнтеграции.ПолучитьПараметрыПодключения(ВыборкаДетальныеЗаписи.Сайт));   

		Соответствие[ВыборкаДетальныеЗаписи.Сайт] = Настройка;
		
	КонецЦикла;  
	
	//настройки статусов
	СоответствиеНастроекСтатусов = Справочники.b24_СтатусыЗадачСайта.ПолучитьНастройкиСтатусовПоМассивуСайтов(Массив);
	
	Для Каждого Настройка Из Соответствие Цикл 
		Настройка.Значение.Вставить("Статусы", СоответствиеНастроекСтатусов[Настройка.Ключ]);
	КонецЦикла;
		
	Возврат Соответствие
	
КонецФункции    

Процедура ЗаписатьДанныеОбмена(Сайт, ДатаНачала, ДатаОкончания, КоличествоВыполненныхЗапросов) Экспорт 
	
	НаборЗаписей = РегистрыСведений.b24_НастройкиИнтеграций.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Сайт.Установить(Сайт);
	НаборЗаписей.Прочитать();
	
	Для Каждого Строка Из НаборЗаписей Цикл 
		
		Строка.ДатаНачалаОбмена 				= ДатаНачала;
		Строка.ДатаПоследнегоУспешногоОбмена 	= ДатаОкончания;
		Строка.КоличествоВыполненныхЗапросов 	= КоличествоВыполненныхЗапросов;
		
	КонецЦикла;
	
	НаборЗаписей.Записать();
	
КонецПроцедуры 

Процедура ЗаписатьСостояниеОбмена(Сайт, Выполняется) Экспорт 
	
	НаборЗаписей = РегистрыСведений.b24_НастройкиИнтеграций.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Сайт.Установить(Сайт);
	НаборЗаписей.Прочитать();
	
	Для Каждого Строка Из НаборЗаписей Цикл 
		
		Строка.Выполняется = Выполняется;

	КонецЦикла;
	
	НаборЗаписей.Записать();
	
КонецПроцедуры 

Функция ОбменВыполняется(Сайт) Экспорт 
	
	Результат = Ложь;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	b24_НастройкиИнтеграций.Сайт КАК Сайт
		|ИЗ
		|	РегистрСведений.b24_НастройкиИнтеграций КАК b24_НастройкиИнтеграций
		|ГДЕ
		|	b24_НастройкиИнтеграций.Сайт = &Сайт
		|	И b24_НастройкиИнтеграций.Выполняется";
	
	Запрос.УстановитьПараметр("Сайт", Сайт);
	
	Результат = Не Запрос.Выполнить().Пустой();
		
	Возврат Результат
	
КонецФункции