
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ДеревоРеквизитов = РеквизитФормыВЗначение("РеквизитыОбъекта");
	КоллекцияРеквизитовОбъекта = ДеревоРеквизитов.Строки;
	
	ОбъектМетаданных = Параметры.Ссылка.Метаданные();
	Для Каждого ОписаниеРеквизита Из ОбъектМетаданных.Реквизиты Цикл
		Реквизит = КоллекцияРеквизитовОбъекта.Добавить();
		ЗаполнитьЗначенияСвойств(Реквизит, ОписаниеРеквизита);
		Реквизит.Пометка = Параметры.Отбор.Найти(ОписаниеРеквизита.Имя) <> Неопределено;
		Реквизит.Синоним = ОписаниеРеквизита.Представление();
	КонецЦикла;
	
	КоллекцияРеквизитовОбъекта.Сортировать("Синоним");
		
	ЗначениеВРеквизитФормы(ДеревоРеквизитов, "РеквизитыОбъекта");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыРеквизитыОбъекта

&НаКлиенте
Процедура РеквизитыОбъектаПометкаПриИзменении(Элемент)
	
	ПриИзмененииФлажка(Элементы.РеквизитыОбъекта, "Пометка");
	УстановитьДоступностьКнопкиВыбора();

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Выбрать(Команда)
	Результат = Новый Структура;
	Результат.Вставить("ВыбранныеРеквизиты", ВыбранныеРеквизиты(РеквизитыОбъекта.ПолучитьЭлементы()));
	Результат.Вставить("ПредставлениеВыбранных", ПредставлениеВыбранныхРеквизитов());

	Закрыть(Результат);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Функция ВыбранныеРеквизиты(КоллекцияРеквизитов)
	
	Результат = Новый Массив;
	
	Для Каждого Реквизит Из КоллекцияРеквизитов Цикл
		ПодчиненныеРеквизиты = Реквизит.ПолучитьЭлементы();
		Если ПодчиненныеРеквизиты.Количество() > 0 Тогда
			СписокВыбранных = ВыбранныеРеквизиты(ПодчиненныеРеквизиты);
			Для Каждого ПодчиненныйРеквизит Из СписокВыбранных Цикл
				Результат.Добавить(Реквизит.Имя + "." + ПодчиненныйРеквизит);
			КонецЦикла;
		Иначе
			Если Реквизит.Пометка Тогда
				Результат.Добавить(Реквизит.Имя);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
		
	Возврат Результат;  
	
КонецФункции

&НаКлиенте
Функция ПредставлениеВыбранныхРеквизитов()
	Результат = СтрСоединить(СинонимыВыбранныхРеквизитов(), ", ");
	Если Результат = "*" Тогда
		Результат = НСтр("ru = 'Все реквизиты'");
	КонецЕсли;
	
	Возврат Результат;
КонецФункции

&НаСервере
Функция СинонимыВыбранныхРеквизитов()
	Результат = Новый Массив;
	
	КоллекцияРеквизитов = РеквизитФормыВЗначение("РеквизитыОбъекта");
	
	ВыбранныеРеквизиты = КоллекцияРеквизитов.Строки.НайтиСтроки(Новый Структура("Пометка", 1));
	Если ВыбранныеРеквизиты.Количество() = КоллекцияРеквизитов.Строки.Количество() Тогда
		Результат.Добавить(НСтр("ru = 'Все'"));
		Возврат Результат;
	КонецЕсли;
	
	Для Каждого Реквизит Из ВыбранныеРеквизиты Цикл
		Результат.Добавить(Реквизит.Синоним);
	КонецЦикла;
	
	ВыбранныеРеквизиты = КоллекцияРеквизитов.Строки.НайтиСтроки(Новый Структура("Пометка", 2));
	Для Каждого Реквизит Из ВыбранныеРеквизиты Цикл
		ПодчиненныеРеквизиты = Реквизит.Строки;
		Для Каждого ПодчиненныйРеквизит Из ПодчиненныеРеквизиты Цикл
			Если ПодчиненныйРеквизит.Пометка Тогда
				Результат.Добавить(Реквизит.Синоним + "." + ПодчиненныйРеквизит.Синоним);
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	Возврат Результат;
КонецФункции

// Устанавливает связанные флажки.
&НаКлиенте
Процедура ПриИзмененииФлажка(ДеревоФормы, ИмяФлажка)
	
	ТекущиеДанные = ДеревоФормы.ТекущиеДанные;
	
	Если ТекущиеДанные[ИмяФлажка] = 2 Тогда
		ТекущиеДанные[ИмяФлажка] = 0;
	КонецЕсли;
	
	Пометка = ТекущиеДанные[ИмяФлажка];

	Если Пометка Тогда 
		
		Для Каждого Элемент Из РеквизитыОбъекта.ПолучитьЭлементы() Цикл 
			
			Если Элемент.Имя <> ТекущиеДанные.Имя Тогда 
				Элемент.Пометка = 0;
			КонецЕсли;
			
		КонецЦикла;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура УстановитьДоступностьКнопкиВыбора()
	Элементы.РеквизитыОбъектаВыбрать.Доступность = ВыбранныеРеквизиты(РеквизитыОбъекта.ПолучитьЭлементы()).Количество() > 0
КонецПроцедуры

#КонецОбласти
